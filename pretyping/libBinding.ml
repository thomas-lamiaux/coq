(************************************************************************)
(*         *      The Rocq Prover / The Rocq Development Team           *)
(*  v      *         Copyright INRIA, CNRS and contributors             *)
(* <O___,, * (see version control and CREDITS file for authors & dates) *)
(*   \VV/  **************************************************************)
(*    //   *    This file is distributed under the terms of the         *)
(*         *     GNU Lesser General Public License Version 2.1          *)
(*         *     (see LICENSE file for the text of the license)         *)
(************************************************************************)

open Declarations
open Names
open EConstr
open Environ
open Evd
open Context
open Util
open Namegen

module RelDecl = Rel.Declaration
open RelDecl

(* ************************************************************************** *)
(*                                 State                                      *)
(* ************************************************************************** *)

let _dbg = CDebug.create ~name:"API" ()

type access_key = int (* type of keys *)

(* state *)
module State =
struct

  (* Monad *)
  type state =
    { env : env;
      sigma : evar_map;
      names : Nameops.Fresh.t;
      subst : Esubst.lift;
    }

  type 'a t = state -> evar_map * 'a

  let return (c : 'a) s = (s.sigma, c)
  let bind x f s = let (sigma, a) = x s in f a { s with sigma = sigma}
  let map (f : 'a -> 'b) (t : 'a t) : 'b t = bind t (fun a -> return @@ f a)
  let run env sigma t =
      let s = {
      env = env;
      sigma = sigma;
      names = Nameops.Fresh.of_list @@ Termops.ids_of_rel_context @@ rel_context env;
      subst = Esubst.el_id;
    }
  in t s

  (* Notation for the monad *)
  let (let@) x f = x f
  let (let*) x f = bind x f

  (* Access the current state *)
  let get_env s   = return s.env s
  let get_sigma s = return s.sigma s
  let get_names s = return s.names s
  let get_state s = return s s
  let get_context s = return (EConstr.of_rel_context @@ Environ.rel_context s.env) s
  let update_sigma s sigma = {s with sigma = sigma}


(** {6 Weaken Functions From the Former Context to the New Context } *)

  let weaken c s = return (Vars.exliftn s.subst c) s

  let weaken_rel decl s =
    return (RelDecl.map_constr (fun t -> snd @@ weaken t s ) decl) s

  let weaken_context cxt s =
    let nb_cxt = List.length cxt in
    let wcxt = List.mapi (fun i x ->
      let n = nb_cxt - i -1 in
      let weak x = Vars.exliftn (Esubst.el_liftn n s.subst) x in
      match x with
      | LocalAssum (na, ty) -> LocalAssum (na, weak ty)
      | LocalDef (na, bd, ty) -> LocalDef (na, weak bd, weak ty)
      ) cxt
    in
    return wcxt s


(** {6 Push Functions } *)

  (* Add variables to the context *)
  let add_names names decl =
    match get_name decl with
    | Anonymous -> names
    | Name id -> Nameops.Fresh.add id names

  let fresh_key s = List.length (snd @@ get_context s)

  let push_old_rel decl s =
  let s' = { s with
      env = EConstr.push_rel decl s.env ;
      names = add_names s.names decl;
      subst = Esubst.el_lift s.subst;
    } in
  return (s', fresh_key s) s'

  let push_fresh_rel decl s =
    let s' = { s with
      env = EConstr.push_rel decl s.env ;
      names = add_names s.names decl;
      subst = Esubst.el_shft 1 (Esubst.el_lift s.subst);
    } in
  return (s', fresh_key s) s'


(** {6 Access Functions } *)

  let get_decl key s =
    let n' = List.length (snd @@ get_context s) - key -1 in
    let decl = RelDecl.map_constr (Vars.lift n') (List.nth (snd @@ get_context s) n') in
    return decl s

  let getters f =
    let get_X key =
      let* decl = get_decl key in
      let* cxt = get_context in
      return (f (List.length cxt - key -1) decl) in

    let geti_X keys pos_key = get_X (List.nth keys pos_key) in

    let getij_X keyss pos_k1 pos_k2 = get_X (List.nth (List.nth keyss pos_k1) pos_k2) in

    let get_Xs keys =
      let* s = get_state in
      return @@ Array.of_list @@ List.map (fun key -> snd @@ get_X key s) keys in

    (get_X, geti_X, getij_X, get_Xs)

  let get_sdecl_term =
    fun n d ->
    match RelDecl.get_value d with
    | Some tm -> Vars.lift 1 tm
    | None -> mkRel (1+n)

  let get_term, geti_term, getij_term, get_terms = getters get_sdecl_term

  let get_type, geti_type, getij_type, get_types = getters (fun _ d -> Vars.lift 1 (RelDecl.get_type d))

  let get_aname, geti_aname, getij_aname, get_anames = getters (fun _ cdecl -> RelDecl.get_annot cdecl)

  (* Print functions *)
  (* let print_state print_constr s =
    List.fold_right_i (fun i x acc ->
        acc ++ str "var | " ++ str (string_of_int (List.length (Environ.rel_context s.env) - i)) ++ str " | " ++
        print_constr s.env s.sigma (RelDecl.get_type x)
      ) 0 (snd @@ get_context s) (str "")

  let print_substitution print_constr s =
      List.fold_right (fun x acc ->
          acc ++ print_constr s.env s.sigma x
        ) s.subst (str "") *)

(** {6 Access Functions } *)

  let list_mapi (f : int -> 'a -> 'b t) (l : 'a list) : 'b list t =
    let rec aux i l acc s =
      match l with
      | [] -> (acc, s)
      | a::l -> let (sigma, t) = (f i a) s in
                aux (i + 1) l (t::acc) (update_sigma s sigma)
    in
  fun s ->
  let (acc, s) = aux 0 l [] s in
  return (List.rev acc) s

  let array_mapi (f : int -> 'a -> 'b t) (ar : 'a array) : ('b array) t =
    fun s ->
    let sigma_ref = ref s.sigma in
    let size_ar = Array.length ar in
    if size_ar = 0
    then (!sigma_ref, [||])
    else begin
      let r = Array.init size_ar (
          fun i ->
          let (sigma, x) = f i ar.(i) (update_sigma s !sigma_ref) in
          sigma_ref := sigma;
          x) in
      (!sigma_ref, r)
    end

  let array_map2i (f : int -> 'a -> 'b -> 'c t) (ar1 : 'a array) (ar2 : 'b array) : ('c array) t =
    fun s ->
    let sigma_ref = ref s.sigma in
    let size_ar = Array.length ar1 in
    if size_ar = 0
    then (!sigma_ref, [||])
    else begin
      let r = Array.init size_ar (
          fun i ->
          let (sigma, x) = f i ar1.(i) ar2.(i) (update_sigma s !sigma_ref) in
          sigma_ref := sigma;
          x) in
      (!sigma_ref, r)
    end

end

open State

(* ************************************************************************** *)
(*                               Naming Schemes                               *)
(* ************************************************************************** *)

let name_hd decl s =
  let name_or_hd = named_hd s.env s.sigma (RelDecl.get_type decl) (RelDecl.get_name decl) in
  return name_or_hd s

type naming_scheme = rel_declaration -> rel_declaration t

(* Keep naming as is, including Anonymous *)
let naming_id decl = return decl

(* Chooses the next Id available from the binder's name.
  If the binder is Anonymous, a name is generated using the head the binder's type. *)
let naming_hd decl =
  let* name_or_hd = name_hd decl in
  return @@ set_name (name_or_hd) decl

let naming_hd_dep dep = if dep then naming_hd else naming_id

let naming_hd_fresh decl =
  let* env = get_env in
  let* sigma = get_sigma in
  let* names = get_names in
  let id = id_of_name_using_hdchar env sigma (RelDecl.get_type decl) (RelDecl.get_name decl) in
  let id = Namegen.mangle_id id in
  let id, avoid = Nameops.Fresh.fresh id names in
  return @@ set_name (Name id) decl

let naming_hd_fresh_dep dep = if dep then naming_hd_fresh else naming_id

(* ************************************************************************** *)
(*                            Fold Functions                                  *)
(* ************************************************************************** *)

(* fold functions for state *)
let fold_right_state f l tp t =
  let rec aux ids1 i l =
    match l with
    | [] -> t (List.rev ids1)
    | a :: l -> tp i a (fun id1 -> aux (f id1 ids1) (i+1) l)
  in
  aux [] 0 l

let fold_left_state f l tp t = fold_right_state f (List.rev l) tp t

let fold_right_state_3 f l tp t =
  let rec aux ids1 ids2 ids3 i l =
    match l with
    | [] -> t (List.rev ids1 , List.rev ids2 , List.rev ids3)
    | a :: l -> tp i a (fun (id1 , id2 , id3) -> aux (f id1 ids1) (f id2 ids2) (f id3 ids3) (i+1) l)
  in
  aux [] [] [] 0 l

let fold_left_state_3 f l tp cc =
  fold_right_state_3 f (List.rev l) tp cc


(* ************************************************************************** *)
(*                             Operations                                     *)
(* ************************************************************************** *)

let ind_relevance ind u =
  let* sigma = get_sigma in
  return @@ ERelevance.make @@ Inductive.relevance_of_ind_body ind (EInstance.kind sigma u)

let whd_decompose_prod_decls t =
  let* env = get_env in
  let* sigma = get_sigma in
  return @@ Reductionops.whd_decompose_prod_decls env sigma t

let decompose_lambda_decls t =
  let* sigma = get_sigma in
  return @@ decompose_lambda_decls sigma t

let decompose_app t =
  let* sigma = get_sigma in
  return @@ decompose_app sigma t

let eta_expand_instantiation inst ctxt =
  let* env = get_env in
  let* sigma = get_sigma in
  return @@ Reductionops.eta_expand_instantiation env sigma inst ctxt

let retyping_sort_of t =
  let* env = get_env in
  let* sigma = get_sigma in
  return @@ Retyping.get_sort_of env sigma t

let typing_checked_appvect f xs s =
  let env = snd @@ get_env s in
  let sigma = snd @@ get_sigma s in
  let (sigma, t) = Typing.checked_appvect env sigma f xs in
  return t (update_sigma s sigma)

let fresh_global ref s =
  let (sigma, t) = fresh_global s.env s.sigma ref in
  return t (update_sigma s sigma)


(* ************************************************************************** *)
(*                            Make Binders                                    *)
(* ************************************************************************** *)

(* Notations to specify functions *)
type freshness = Fresh | Old
type binder = Lambda | Prod

let wrap_binder b =
  match b with
  | Lambda -> mkLambda_or_LetIn
  | Prod -> mkProd_or_LetIn

let wrap_decl f fresh naming_scheme decl cc s =
  match fresh with
    | Fresh -> let (sigma, decl) = naming_scheme decl s in
               let (sigma, (s', k)) = push_fresh_rel decl s in
               let (sigma, v) = cc k s' in
               sigma, f decl v
    | Old -> let (sigma, decl) = weaken_rel decl s in
             let (sigma, decl) = naming_scheme decl s in
             let (sigma, (s', k)) = push_old_rel decl s in
             let (sigma, v) = cc k s' in
             sigma, f decl v

let add_decl a = wrap_decl (fun _ x -> x) a

let build_binder binder = wrap_decl (wrap_binder binder)
let make_binder binder naming_scheme na ty = build_binder binder Fresh naming_scheme (LocalAssum (na, ty))
let keep_binder binder naming_scheme na ty = build_binder binder Old naming_scheme (LocalAssum (na, ty))

let build_binder_opt binder = wrap_decl (fun decl v -> Option.map (wrap_binder binder decl) v)
let make_binder_opt binder naming_scheme na ty = build_binder_opt binder Fresh naming_scheme (LocalAssum (na, ty))
let keep_binder_opt binder naming_scheme na ty = build_binder_opt binder Old naming_scheme (LocalAssum (na, ty))

(* 2. Iterate binders *)
let read_context binder cxt = fold_left_state (fun a l -> a :: l) cxt (fun _ -> binder)

let add_context fresh naming_scheme = read_context (add_decl fresh naming_scheme)
let closure_context binder fresh naming_scheme = read_context (build_binder binder fresh naming_scheme)
let closure_context_opt binder fresh naming_scheme = read_context (build_binder_opt binder fresh naming_scheme)



(* seperate var and letin in key_vars / key_letins / key_both *)

let read_context_sep binder cxt =
  fold_left_state_3 List.append cxt (fun _ decl cc ->
    let@ key = binder decl in
    match decl with
    | LocalAssum _ -> cc ([key],[],[key])
    | LocalDef _   -> cc ([],[key],[key])
  )

let add_context_sep fresh naming_scheme = read_context_sep (add_decl fresh naming_scheme)
let closure_context_sep binder fresh naming_scheme = read_context_sep (build_binder binder fresh naming_scheme)
let closure_context_sep_opt binder fresh naming_scheme = read_context_sep (build_binder_opt binder fresh naming_scheme)

let map_second f (a, b) = (a, f b)

let build_binder_opt_prod binder = wrap_decl (fun decl v -> Option.map (map_second @@ wrap_binder binder decl) v)
let closure_context_sep_opt_prod binder fresh naming_scheme = read_context_sep (build_binder_opt_prod binder fresh naming_scheme)

(* takes a continuation after binder var and letin to add fresh binders and decide what to do with the keys *)
let read_by_decl cxt binder cc_letin cc_var =
  fold_left_state List.append cxt (fun pos_decl decl cc ->
    let@ key = binder decl in
    match decl with
    | LocalDef _   -> cc_letin pos_decl key cc
    | LocalAssum _ -> cc_var   pos_decl key cc
  )

(* ************************************************************************** *)
(*                           Functions on Inductive                           *)
(* ************************************************************************** *)

let get_args mdecl u (cxt, ty) =
  let nb_params_letin = List.length mdecl.mind_params_ctxt in
  let (_, args) = List.chop nb_params_letin (List.rev cxt) in
  let args = Vars.subst_instance_context u @@ EConstr.of_rel_context @@ List.rev args in
  let* (hd, xs) = decompose_app (Vars.subst_instance_constr u @@ EConstr.of_constr ty) in
  let indices = Array.sub xs mdecl.mind_nparams (Array.length xs - mdecl.mind_nparams) in
  return (args, indices)

let iterate_ctors mdecl ind u tp cc =
  let* ctors = array_mapi (fun _ -> get_args mdecl u) ind.mind_nf_lc in
  fold_right_state (fun a l -> a :: l) (Array.to_list ctors) tp cc

let make_ind ((kn, pos_ind), u) key_uparams key_nuparams key_indices =
  let tInd = mkIndU ((kn, pos_ind), u) in
  let* inst_ind = get_terms (key_uparams @ key_nuparams @ key_indices) in
  return @@ mkApp (tInd, inst_ind)

let make_cst ((kn, pos_ind), u) pos_ctor key_uparams key_nuparams args =
  let tCst = mkConstructU (((kn, pos_ind), 1+pos_ctor), u) in
  let* params = get_terms (key_uparams @ key_nuparams) in
  return @@ mkApp (tCst, (Array.concat [params; args]))

(* make fix *)
let make_fix ind_bodies focus fix_rarg fix_name fix_type tmc =
  (* data fix *)
  let rargs = List.mapi fix_rarg ind_bodies in
  let fix_names = List.mapi fix_name ind_bodies in
  let* fix_types = list_mapi fix_type ind_bodies in
  (* update context continuation *)
  let fix_context = List.rev @@ List.map2_i (fun i na ty -> LocalAssum (na, Vars.lift i ty)) 0 fix_names fix_types in
  let@ key_Fix = add_context Fresh naming_id fix_context in
  let* fix_bodies = list_mapi (fun pos_list ind -> tmc (key_Fix, pos_list, ind)) ind_bodies in
  (* result *)
  return @@ EConstr.mkFix ((Array.of_list rargs, focus), (Array.of_list fix_names, Array.of_list fix_types, Array.of_list fix_bodies))

let get_indices indb u =
  let indices, _ = List.chop indb.mind_nrealdecls indb.mind_arity_ctxt in
  weaken_context (Vars.subst_instance_context u (EConstr.of_rel_context indices))

(* make match *)
let make_case_or_projections naming_vars mdecl ind indb u key_uparams key_nuparams params indices mk_case_pred case_relevance tm_match tc =
  let* env = get_env in
  let case_info = Inductiveops.make_case_info env ind RegularStyle in
  let case_invert =
    if Typeops.should_invert_case env (Unsafe.to_relevance case_relevance) case_info
    then Constr.CaseInvert {indices=indices}
    else Constr.NoInvert
  in

  let case_pred =
    (* indices *)
    let* indices = get_indices indb u in
    let@ key_fresh_indices, _ , key_both = add_context_sep Fresh naming_vars indices in
    (* new var *)
    let* inst_ind = get_terms (key_uparams @ key_nuparams @ key_fresh_indices) in
    let ty_var = mkApp (mkIndU (ind, u), inst_ind) in
    let* env = get_env in
    let name_var_match = make_annot Anonymous (Inductiveops.relevance_of_inductive env (ind, u)) in
    let@ key_var_match = add_decl Fresh naming_vars (LocalAssum (name_var_match, ty_var)) in
    (* return type *)
    let* fresh_annot = get_anames (key_both @ [key_var_match]) in
    let* return_type = mk_case_pred key_fresh_indices key_var_match in
    return @@ ((fresh_annot, return_type), case_relevance)
  in

  let branch pos_ctor ctor =
    let* args = get_args mdecl u ctor in
    let args = fst args in
    let@ key_args, key_letin, key_both = add_context_sep Old naming_vars args in
    let* branches_body = tc (key_args, key_letin, key_both, pos_ctor) in
    let* names_args = get_anames key_both in
    return (names_args, branches_body)
  in

  let* case_info, pred, case_invert, c, branches =
    let* case_pred = case_pred in
    let* s = get_state in
    let* branches = array_mapi branch indb.mind_nf_lc in
    let* env = get_env in
    let* sigma = get_sigma in
    return @@ EConstr.expand_case env sigma (case_info, u, params, case_pred, case_invert, tm_match, branches)
  in

  let* env = get_env in
  let* sigma = get_sigma in
  return @@ Inductiveops.simple_make_case_or_project env sigma case_info pred case_invert c branches
