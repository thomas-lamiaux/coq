.. _inductive:

Inductive types and recursive functions
=======================================

The :cmd:`Inductive` command allows defining types by cases on the form of the
:term:`inhabitants <inhabitant>` of the type. These constructors can recursively
have arguments in the type being defined.  In contrast, in types defined by the
:cmd:`Variant` command, such recursive references are not permitted.
Inductive types include natural numbers,
lists and well-founded trees. Inhabitants of inductive types can
recursively nest only a finite number of constructors. So, they are
well-founded. This distinguishes them from :cmd:`CoInductive` types,
such as streams, whose constructors can be infinitely nested. In Rocq,
:cmd:`Variant` types thus correspond to the common subset of inductive
and coinductive types that are non-recursive.

Due to the recursive structure of inductive types, functions on
inductive types generally must be defined
recursively using the :n:`fix` expression (see :n:`@term_fix`) or the
:cmd:`Fixpoint` command.

.. _gallina-inductive-definitions:

Inductive types
---------------

.. cmd:: Inductive @inductive_definition {* with @inductive_definition }
         Inductive @record_definition {* with @record_definition }

   .. insertprodn inductive_definition constructor

   .. prodn::
      inductive_definition ::= @ident {? @cumul_univ_decl } {* @binder } {? %| {* @binder } } {? : @type } := {? {? %| } {+| @constructor } } {? @decl_notations }
      constructor ::= {* #[ {+, @attribute } ] } @ident {* @binder } {? of {+& @term99 } } {? @of_type_inst }

   Constructor :n:`@ident`\s can come with :n:`@binder`\s, in which case
   the actual type of the constructor is :n:`forall {* @binder }, @type`,
   where :n:`@type` is its result type. For inductive types without indices,
   this result type can be omitted and is inferred from the declaration.

   :n:`{? of {+& @term99 } }` `of T1 & ... & Tn` is syntactic sugar for anonymous binders `(_ : T1) ... (_ : Tn)`.

   Constructor arguments can be declared using three alternative syntaxes.

   .. example:: Constructor argument syntaxes

      The following declarations use a full constructor type, anonymous
      binders, and :n:`of`, respectively, to describe pairs of booleans.

      .. rocqtop:: in

         Inductive bool_pair_type : Type :=
         | pair_type : bool -> bool -> bool_pair_type.

         Inductive bool_pair_binders : Type :=
         | pair_binders (_ : bool) (_ : bool).

         Inductive bool_pair_of : Type :=
         | pair_of of bool & bool.

   Mutually inductive types can be defined by including multiple :n:`@inductive_definition`\s.
   The :n:`@ident`\s are simultaneously added to the global environment before
   the types of constructors are checked. Each :n:`@ident` can be used
   independently thereafter. However, the automatically generated induction
   principles do not provide induction hypotheses for the other mutually
   defined types. Use the :cmd:`Scheme` command to generate mutual induction
   principles. See :ref:`mutually_inductive_types`.

   .. example:: Mutually inductive predicates

      The following predicates characterize even and odd natural numbers.
      Each refers to the other, so they are declared together using :n:`with`.

      .. rocqtop:: in

         Inductive even : nat -> Prop :=
         | even_zero : even 0
         | even_succ : forall n, odd n -> even (S n)
         with odd : nat -> Prop :=
         | odd_succ : forall n, even n -> odd (S n).

   If the entire inductive definition is parameterized with :n:`@binder`\s, those
   :gdef:`inductive parameters <inductive parameter>` correspond
   to a local context in which the entire set of inductive declarations is interpreted.
   Within a mutual declaration, all inductive types must have the same
   parameter binders, with the same names and types in the same order.
   See :ref:`parametrized-inductive-types`.

   :n:`{? %| {* @binder } }`
     The :n:`|` separates uniform and non-uniform parameters.
     See :flag:`Uniform Inductive Parameters` for more details.

   The :cmd:`Inductive` command supports the :attr:`universes(polymorphic)`,
   :attr:`universes(template)`, :attr:`universes(cumulative)`,
   :attr:`universes(collapse_sort_variables)`, :attr:`bypass_check(universes)`,
   :attr:`bypass_check(positivity)`, :attr:`private(matching)` and
   :attr:`schemes` attributes.

   When record syntax (``{ ... }``) is used, :attr:`projections(primitive)`
   is also supported, while :attr:`private(matching)` and the :n:`bypass_check`
   attributes are not supported. In record syntax, the optional :n:`as @ident`
   part specifies the name to use for inhabitants of the record in the type
   of projections.


.. _automatic-prop-lowering:

Automatic Prop lowering
~~~~~~~~~~~~~~~~~~~~~~~

When an inductive type is declared without an explicit sort, it is put in the
smallest sort which permits large elimination, that is, elimination into
:g:`Set` or :g:`Type` (excluding :g:`SProp` from the inferred sorts).
For :ref:`empty and singleton <Empty-and-singleton-elimination>` types this
means they are declared in :g:`Prop`. An empty type has no constructors;
a singleton type has one constructor whose arguments, if any, are proofs.

.. example:: Inferring sorts

   Neither declaration below specifies a sort. The first has one constructor
   without arguments, so Rocq declares it in :g:`Prop`. The second stores an
   element of :g:`A : Type`, so :g:`inferred_box A` belongs to :g:`Type`.

   .. rocqtop:: in

      Inductive inferred_singleton := inferred_singleton_intro.
      Inductive inferred_box (A : Type) :=
      | inferred_box_intro : A -> inferred_box A.

   .. rocqtop:: all

      Check inferred_singleton.
      Check inferred_box Type.

Positivity Condition
~~~~~~~~~~~~~~~~~~~~

To be accepted, an inductive type must satisfy the *strict positivity condition*.
See :ref:`positivity` for the exact condition, including for mutual and nested inductive types.
Informally, the inductive type being defined must not occur to the left of
an arrow within an argument type, even under another arrow.
It may occur to the right of an arrow whose domain does not contain that type.

.. exn:: Non strictly positive occurrence of @ident in @type.

   An occurrence of the inductive type in a constructor argument violates the
   strict positivity condition. Positivity checking can be disabled using the
   :flag:`Positivity Checking` flag or the :attr:`bypass_check(positivity)`
   attribute (see :ref:`controlling-typing-flags`).

.. example:: An inductive type in the domain of a function argument

   The constructor below takes a function whose input has type :g:`negative`,
   the type being defined. This occurrence is to the left of an arrow in the
   constructor argument type :g:`negative -> nat`, so it is rejected.

   .. rocqtop:: all

      Fail Inductive negative : Type :=
      | negative_intro : (negative -> nat) -> negative.

.. example:: Two arrows do not restore strict positivity

   Nesting :g:`double_negative -> nat` in another function domain does not
   make the recursive occurrence strictly positive. The type being defined
   still occurs to the left of an arrow within the constructor argument type,
   so Rocq rejects the following definition as well:

   .. rocqtop:: all

      Fail Inductive double_negative : Type :=
      | double_negative_intro : ((double_negative -> nat) -> nat) -> double_negative.

Constructor conclusions must also satisfy a separate requirement: they must
return the inductive type being defined.

.. exn:: The conclusion of @type is not valid; it must be built from @ident.

   The conclusion of each constructor type must be the inductive type
   :n:`@ident` being defined, applied to its parameters and indices when present.

   .. example:: A constructor with an invalid conclusion

      The constructor below returns :g:`nat` instead of the type
      :g:`invalid_conclusion` being defined, so Rocq rejects the declaration.

      .. rocqtop:: all

         Fail Inductive invalid_conclusion : Type :=
         | invalid_conclusion_intro : nat.

Eliminators
~~~~~~~~~~~

By default, Rocq automatically generates eliminators, also known as
:gdef:`induction principles <induction principle>`, for an inductive type,
depending on the sort to which it belongs and the sorts into which it can be
eliminated.

The induction principles are named :n:`@ident`\ ``_rect``, :n:`@ident`\ ``_ind``,
:n:`@ident`\ ``_rec`` and :n:`@ident`\ ``_sind``, corresponding respectively to
elimination into :g:`Type`, :g:`Prop`, :g:`Set` and :g:`SProp`.
Their types express structural induction or recursion over objects of type
:n:`@ident`. These :term:`constants <constant>` are generated when permitted
by the elimination restrictions. For instance, :n:`@ident`\ ``_rect`` may not
be generated when :n:`@ident` is a proposition.

.. example:: Generated eliminators

   The eliminators :g:`bool_ind` and :g:`bool_rect` each require cases for
   :g:`true` and :g:`false`. Their predicates take values in :g:`Prop` and
   :g:`Type`, respectively.

   .. rocqtop:: all

      Check bool_ind.
      Check bool_rect.

Variants of these eliminators as well as other schemes can be generated with the
:cmd:`Scheme` command.
See also :ref:`automatic-declaration-of-schemes` for the flags and attribute
controlling the automatic generation of these schemes.

.. flag:: Dependent Proposition Eliminators

   This flag controls whether automatically generated induction principles
   for inductive types explicitly declared in :g:`Prop` are dependent.
   It defaults to off: the result type does not depend on the proof being
   eliminated. When the flag is on, the result type may depend on that proof.
   For example, when eliminating :g:`p : P`, a dependent principle can
   establish :g:`Q p`, whereas a non-dependent principle establishes a result
   whose type does not depend on :g:`p`. The flag does not relax the
   restrictions on the sorts into which the inductive type can be eliminated.

   .. example:: Non-dependent and dependent principles

      The two propositions below have the same form, but the generated
      principles differ. In :g:`plain_proof_ind`, :g:`P` is a proposition.
      In :g:`dependent_proof_ind`, :g:`P` is a predicate on proofs of
      :g:`dependent_proof`.

      .. rocqtop:: in

         Unset Dependent Proposition Eliminators.
         Inductive plain_proof : Prop := plain_intro.

      .. rocqtop:: all

         Check plain_proof_ind.

      .. rocqtop:: in

         Set Dependent Proposition Eliminators.
         Inductive dependent_proof : Prop := dependent_intro.

      .. rocqtop:: all

         Check dependent_proof_ind.

      .. rocqtop:: in

         Unset Dependent Proposition Eliminators.

   The flag is consulted when the inductive type is declared; changing it
   does not alter principles already generated. Types
   :ref:`automatically lowered to Prop <automatic-prop-lowering>` already
   receive dependent principles, even when the flag is off.
   For other sorts, generated principles are also dependent whenever the
   inductive type permits dependent elimination. Some records using
   :flag:`Primitive Projections` do not permit it.

   With :cmd:`Scheme`, dependent induction corresponds to ``Induction`` and
   non-dependent induction to ``Minimality`` in :n:`@scheme_type`.
   Explicit declarations through this command are not affected by the flag.
   The flag can also affect the names automatically chosen for variables
   introduced by tactics such as :tacn:`destruct`.

Examples of inductive types and eliminators
--------------------------------------------

The following sections show examples of simple inductive types,
simple indexed inductive types, parameterized inductive types,
mutually defined inductive types and nested inductive types.

.. _simple-inductive-types:

Simple inductive types
~~~~~~~~~~~~~~~~~~~~~~

A simple inductive type belongs to a universe that is a simple :n:`@sort`.

.. example::

   The set of natural numbers is defined as:

   .. rocqtop:: reset all

      Inductive nat : Set :=
      | O : nat
      | S : nat -> nat.

   The type nat is defined as the least :g:`Set` containing :g:`O` and closed by
   the :g:`S` constructor. The names :g:`nat`, :g:`O` and :g:`S` are added to the
   global environment.

   This definition generates four :term:`induction principles <induction principle>`:
   :g:`nat_rect`, :g:`nat_ind`, :g:`nat_rec` and :g:`nat_sind`. The type of :g:`nat_ind` is:

   .. rocqtop:: all

      Check nat_ind.

   This is the well known structural induction principle over natural
   numbers, i.e. the second-order form of Peano’s induction principle. It
   allows proving universal properties of natural numbers (:g:`forall
   n:nat, P n`) by induction on :g:`n`.

   The types of :g:`nat_rect`, :g:`nat_rec` and :g:`nat_sind` are similar, except that they
   apply to, respectively, :g:`(P:nat->Type)`, :g:`(P:nat->Set)` and :g:`(P:nat->SProp)`. They correspond to
   primitive induction principles (allowing dependent types) respectively
   over sorts ``Type``, ``Set`` and ``SProp``.

In the case where inductive types don't have indices (the next section
gives an example of indices), a constructor can be defined
by giving the type of its arguments alone.

.. example::

   .. rocqtop:: reset none

      Reset nat.

   .. rocqtop:: in

      Inductive nat : Set := O | S (_:nat).

Simple indexed inductive types
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

An indexed inductive definition has arguments called *indices*, whose
instantiations may differ between constructor conclusions. Its type is an
arity of the form :g:`forall x1 : T1, ... forall xn : Tn, s`, where :g:`s`
is a :n:`@sort`.

.. example:: Typed expressions

   The following type describes expressions whose result type is given by
   the index. Natural-number literals and addition have index :g:`nat`,
   while Boolean literals, comparisons, and equality tests have index
   :g:`bool`. A conditional has the same index as its two branches.

   .. rocqtop:: in

      Inductive expr : Type -> Type :=
      | bool_literal : bool -> expr bool
      | nat_literal : nat -> expr nat
      | op_addition : expr nat -> expr nat -> expr nat
      | op_comparison : expr nat -> expr nat -> expr bool
      | op_equality ty : expr ty -> expr ty -> expr bool
      | op_if ty : expr bool -> expr ty -> expr ty -> expr ty.

   Thus, :g:`expr nat` contains expressions producing natural numbers, and
   :g:`expr bool` contains expressions producing Booleans. The constructors
   constrain the types of their arguments: :g:`op_addition` and
   :g:`op_comparison` both take two expressions with index :g:`nat`, but
   their result indices differ.

   The constructor :g:`op_equality` takes two expressions with the same
   index :g:`ty` and returns an expression with index :g:`bool`. Unlike
   :g:`op_comparison`, its operands are not restricted to index :g:`nat`.
   The constructor :g:`op_if` takes a condition with index :g:`bool` and
   two branches with the same index :g:`ty`, and returns :g:`expr ty`.
   In both constructors, :g:`ty` ranges over types: constructor conclusions
   can contain a variable index as well as a fixed index such as :g:`nat`
   or :g:`bool`.

   .. rocqtop:: all

      Check expr_ind.

   The principle :g:`expr_ind` establishes that a predicate holds for every
   typed expression. The predicate ranges over both the result type and the
   expression, with one case for each constructor. The addition and
   comparison cases each provide two induction hypotheses at index :g:`nat`,
   one for each operand; their conclusions establish the predicate at indices
   :g:`nat` and :g:`bool`, respectively. The equality case provides two
   induction hypotheses at index :g:`ty` and concludes at index :g:`bool`.
   The conditional case provides three induction hypotheses: one at index
   :g:`bool` for the condition and two at index :g:`ty` for the branches.
   Its conclusion establishes the predicate at index :g:`ty`.

.. _parametrized-inductive-types:

Parameterized inductive types
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

In the previous example, constructor conclusions instantiate the index
with :g:`nat`, :g:`bool`, or the constructor-bound variable :g:`ty`.
Parameters, in contrast, are :n:`@binder`\s shared by all constructors.
Every constructor conclusion uses the parameters bound by the declaration
as their instantiations.

Parameters need not have the same instantiation in recursive occurrences
within constructor arguments. A parameter is uniform when it has the same
instantiation in all recursive occurrences as in the constructor conclusions;
otherwise, it is non-uniform. The distinction is determined by how the
parameter is used in the definition.

An inductive definition can use indices in place of parameters, but this
can affect its sort and elimination principles. Constructors must then
quantify explicitly over the arguments that were parameters; this
quantification can impose higher universe requirements. The induction
predicate is quantified over the indices, whereas a uniform parameter is
fixed before the induction predicate is introduced.

Uniform parameters
++++++++++++++++++

In every constructor conclusion, the inductive type must be applied to
the parameters bound by the declaration.
In recursive occurrences of the inductive type within constructor arguments,
uniform parameters must be instantiated with the same values as in the
constructor conclusion.

.. example::

   A typical example is the definition of polymorphic lists:

   .. rocqtop:: all

      Inductive list (A:Set) : Set :=
      | nil : list A
      | cons : A -> list A -> list A.

   In the type of :g:`nil` and :g:`cons`, we write ":g:`list A`" and not
   just ":g:`list`". The constructors :g:`nil` and :g:`cons` have these types:

   .. rocqtop:: all

      Check nil.
      Check cons.

   Observe that the induction principles are also quantified with :g:`(A:Set)`,
   for example:

   .. rocqtop:: all

      Check list_ind.

   Once again, the names of the constructor arguments and the type of the conclusion can be omitted:

   .. rocqtop:: none

      Reset list.

   .. rocqtop:: in

      Inductive list (A:Set) : Set := nil | cons (_:A) (_:list A).

Non-uniform parameters
++++++++++++++++++++++

In constructor conclusions, a non-uniform parameter must be instantiated
with the parameter bound by the declaration. In recursive occurrences within
constructor arguments, it may be instantiated with a different term.
The induction principle must therefore allow its predicate to vary with that
parameter, whereas a uniform parameter is fixed throughout induction.

.. example:: A non-uniform parameter in recursive occurrences

   In the power list type :g:`plist`, the constructor :g:`pcons` takes a recursive argument of
   type :g:`plist (A * A)` and returns :g:`plist A`. Thus, :g:`A` is a
   non-uniform parameter: it is instantiated with :g:`A * A` in the
   recursive argument and with :g:`A` in the constructor conclusion.

   .. rocqtop:: in

      Inductive plist (A : Set) : Set :=
      | pnil : plist A
      | pcons : A -> plist (A * A) -> plist A.

   Unlike :g:`list_ind`, whose predicate concerns lists over one fixed
   type :g:`A`, :g:`plist_ind` takes a predicate over all element types in :g:`Set`.
   Its recursive premise uses that predicate at :g:`A * A`.

   .. rocqtop:: all

      Check plist_ind.

.. example:: Invalid parameter instantiation in constructor conclusions

   Even a non-uniform parameter must be instantiated with the parameter
   bound by the declaration in each constructor conclusion. The following
   declaration is rejected because
   its constructors return :g:`listw (A * A)` instead of :g:`listw A`:

   .. rocqtop:: all

      Fail Inductive listw (A : Set) : Set :=
      | nilw : listw (A * A)
      | consw : A -> listw (A * A) -> listw (A * A).

The separator :n:`|` specifies which parameters are abstracted during
constructor checking. Parameters before it are uniform by construction and
are omitted from recursive occurrences within the declaration. Parameters
after it are supplied explicitly and may be used uniformly or non-uniformly.
Outside the declaration, the inductive type takes both groups as arguments.

.. example:: Using :n:`|` in parameters

   A parameter after :n:`|` can still be uniform, as :g:`A` is here:

   .. rocqtop:: in

      Inductive explicit_list | (A : Type) : Type :=
      | explicit_nil : explicit_list A
      | explicit_cons : A -> explicit_list A -> explicit_list A.

   .. rocqtop:: all

      Check explicit_list_ind.

   As in :g:`list_ind`, :g:`A` is fixed before the induction predicate is
   introduced.

   However, placing :g:`A` before :n:`|` prevents the non-uniform
   instantiation used in :g:`plist`: during constructor checking,
   :g:`uniform_plist` takes no explicit parameter, so applying it to
   :g:`A * A` is rejected.

   .. rocqtop:: all

      Fail Inductive uniform_plist (A : Set) | : Set :=
      | uniform_pnil : uniform_plist
      | uniform_pcons : A -> uniform_plist (A * A)%type -> uniform_plist.

.. flag:: Uniform Inductive Parameters

   This flag determines the implicit position of :n:`|` when the declaration
   does not contain an explicit separator. It defaults to off, corresponding
   to placing :n:`|` before all parameters. When on, it corresponds to placing
   :n:`|` after all parameters.

   When the flag is on, all parameters are abstracted during constructor
   checking and are uniform by construction. They are therefore omitted
   from recursive occurrences within the declaration. When the flag is off,
   parameters are supplied explicitly, as in :g:`list A` and
   :g:`plist (A * A)` above, and may be used uniformly or non-uniformly.

   .. example:: Declaring uniform parameters implicitly

      With the flag on, :g:`A` is fixed throughout the declaration, so
      the recursive occurrence is written :g:`list3` rather than
      :g:`list3 A`.

      .. rocqtop:: in

         Set Uniform Inductive Parameters.
         Inductive list3 (A : Set) : Set :=
         | nil3 : list3
         | cons3 : A -> list3 -> list3.
         Unset Uniform Inductive Parameters.

      This is equivalent to declaring the type in a section with
      :g:`A` in the context (see :ref:`section-mechanism`):

      .. rocqtop:: in reset

         Section list3.
         Context (A : Set).
         Inductive list3 : Set :=
         | nil3 : list3
         | cons3 : A -> list3 -> list3.
         End list3.

   An explicit :n:`|` overrides the flag. Only the parameters before the
   separator are abstracted during constructor checking; those after it
   are supplied explicitly and may be used uniformly or non-uniformly.

   .. example:: Explicit non-uniform parameters with the flag on

      Placing :n:`|` before :g:`A` allows it to be non-uniform even with
      the flag on. As in :g:`plist`, the recursive argument instantiates
      :g:`A` with :g:`A * A`.

      .. rocqtop:: in

         Set Uniform Inductive Parameters.
         Inductive explicit_plist | (A : Set) : Set :=
         | explicit_pnil : explicit_plist A
         | explicit_pcons : A -> explicit_plist (A * A) -> explicit_plist A.
         Unset Uniform Inductive Parameters.

.. _mutually_inductive_types:

Mutually defined inductive types
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

.. todo: combine with the very similar tree/forest example in reasoning-inductives.rst

The automatically generated induction principles do not provide induction
hypotheses for the other mutually defined types. Use the :cmd:`Scheme` command
to generate mutual induction principles.

.. example:: Mutually defined inductive types

   A typical example of mutually inductive data types is trees and
   forests. We assume two types :g:`A` and :g:`B` that are given as variables. The types can
   be declared like this:

   .. rocqtop:: in

      Parameters A B : Set.

      Inductive tree : Set := node : A -> forest -> tree

      with forest : Set :=
      | leaf : B -> forest
      | cons : tree -> forest -> forest.

   This declaration automatically generates eight induction principles. They are not the most
   general principles, but they correspond to each inductive part seen as a single inductive definition.

   To illustrate this point on our example, here are the types of :g:`tree_rec`
   and :g:`forest_rec`.

   .. rocqtop:: all

      Check tree_rec.

      Check forest_rec.

   Assume we want to parameterize our mutual inductive definitions with the
   two type variables :g:`A` and :g:`B`, the declaration should be
   done as follows:

   .. rocqdoc::

      Inductive tree (A B:Set) : Set := node : A -> forest A B -> tree A B

      with forest (A B:Set) : Set :=
      | leaf : B -> forest A B
      | cons : tree A B -> forest A B -> forest A B.

   Assume we define an inductive definition inside a section
   (cf. :ref:`section-mechanism`). When the section is closed, the variables
   declared in the section and occurring free in the declaration are added as
   parameters to the inductive definition.

.. seealso::
   A generic command :cmd:`Scheme` is useful to build automatically various
   mutual induction principles.

.. _nested-inductive-types:

Nested inductive types
~~~~~~~~~~~~~~~~~~~~~~

An occurrence of an inductive type in a constructor argument is called
*nested* when it appears as an argument to another inductive type. By
extension, an inductive type with nested recursive occurrences is itself
called nested. Such occurrences can be accepted if they satisfy the
:ref:`nested positivity condition <nested-positivity>`.

To generate induction hypotheses for nested recursive arguments, Rocq uses
an ``All`` predicate and its associated theorem registered for the type used
for nesting. The predicate expresses that the induction predicate holds for
each recursive subterm contained in the nested argument. Without these
registered definitions, the generated eliminator has no induction hypothesis
for that argument.

For an inductive type, these definitions can be generated and registered
with :cmd:`Scheme All` before declaring the nested inductive type. See
:ref:`nested-inductive-eliminators` for details on generating and registering
them.

.. example:: Rose trees

   A rose tree has leaves labeled with elements of :g:`A` and nodes containing
   lists of child trees. In the argument type :g:`list (RoseTree A)`, the
   recursive occurrence :g:`RoseTree A` is nested in :g:`list`.
   Informally, this is permitted because the element type is a strictly positive
   uniform parameter of :g:`list`.

   .. rocqtop:: in

      Scheme All for list.

      Inductive RoseTree A : Type :=
      | RTleaf (a : A) : RoseTree A
      | RTnode (l : list (RoseTree A)) : RoseTree A.

   The generated principle uses :g:`list_all` to express the induction
   hypotheses for the child trees:

   .. rocqtop:: all

      Check RoseTree_ind.

   For a predicate :g:`P : RoseTree A -> Prop`, the :g:`RTnode` case
   requires :g:`list_all (RoseTree A) P l`, expressing that :g:`P` holds for
   every tree in :g:`l`, to establish :g:`P (RTnode A l)`.
   The definition of :g:`list_all` has a case for the empty list and a case
   requiring :g:`P` for the head and :g:`list_all (RoseTree A) P` for
   the tail:

   .. rocqtop:: all

      Print list_all.

.. index::
   single: fix

Recursive functions: fix
------------------------

.. insertprodn term_fix fixannot

.. prodn::
   term_fix ::= let fix @fix_decl in @term
   | fix @fix_decl {? {+ with @fix_decl } for @ident }
   fix_decl ::= @ident {* @binder } {? @fixannot } {? : @type } := @term
   fixannot ::= %{ struct @ident %}
   | %{ wf @one_term @ident %}
   | %{ measure @one_term {? @ident } {? @one_term } %}


The expression ":n:`fix @ident__1 @binder__1 : @type__1 := @term__1 with … with @ident__n @binder__n : @type__n := @term__n for @ident__i`" denotes the
:math:`i`-th component of a block of functions defined by mutual structural
recursion. It is the local counterpart of the :cmd:`Fixpoint` command. When
:math:`n=1`, the ":n:`for @ident__i`" clause is omitted.

The association of a single fixpoint and a local definition have a special
syntax: :n:`let fix @ident {* @binder } := @term in` stands for
:n:`let @ident := fix @ident {* @binder } := @term in`. The same applies for cofixpoints.

Some options of :n:`@fixannot` are only supported in specific constructs.  :n:`fix` and :n:`let fix`
only support the :n:`struct` option, while :n:`wf` and :n:`measure` are only supported in
commands such as :cmd:`Fixpoint` (with the :attr:`program` attribute) and :cmd:`Function`.

.. todo explanation of struct: see text above at the Fixpoint command, also
   see https://github.com/rocq-prover/rocq/pull/12936#discussion_r510716268 and above.
   Consider whether to move the grammar for fixannot elsewhere

.. _Fixpoint:

Top-level recursive functions
-----------------------------

This section describes the primitive form of definition by recursion over
inductive objects. See the :cmd:`Function` command for more advanced
constructions.

.. cmd:: Fixpoint @fix_definition {* with @fix_definition }

   .. insertprodn fix_definition fix_definition

   .. prodn::
      fix_definition ::= @ident_decl {* @binder } {? @fixannot } {? : @type } {? := @term } {? @decl_notations }

   Allows defining functions by pattern matching over inductive
   objects using a fixed point construction. The meaning of this declaration is
   to define :n:`@ident` as a recursive function with arguments specified by
   the :n:`@binder`\s such that :n:`@ident` applied to arguments
   corresponding to these :n:`@binder`\s has type :n:`@type`, and is
   equivalent to the expression :n:`@term`. The type of :n:`@ident` is
   consequently :n:`forall {* @binder }, @type` and its value is equivalent
   to :n:`fun {* @binder } => @term`.

   This command accepts the :attr:`program`,
   :attr:`bypass_check(universes)`, and :attr:`bypass_check(guard)` attributes.

   To be accepted, a :cmd:`Fixpoint` definition has to satisfy syntactical
   constraints on a special argument called the decreasing argument. They
   are needed to ensure that the :cmd:`Fixpoint` definition always terminates.
   The point of the :n:`{struct @ident}` annotation (see :n:`@fixannot`) is to
   let the user tell the system which argument decreases along the recursive calls.

   The :n:`{struct @ident}` annotation may be left implicit, in which case the
   system successively tries arguments from left to right until it finds one
   that satisfies the decreasing condition.

   :cmd:`Fixpoint` without the :attr:`program` attribute does not support the
   :n:`wf` or :n:`measure` clauses of :n:`@fixannot`. See :ref:`program_fixpoint`.

   The :n:`with` clause allows simultaneously defining several mutual fixpoints.
   It is especially useful when defining functions over mutually defined
   inductive types.  Example: :ref:`Mutual Fixpoints<example_mutual_fixpoints>`.

   If :n:`@term` is omitted, :n:`@type` is required and Rocq enters proof mode.
   This can be used to define a term incrementally, in particular by relying on the :tacn:`refine` tactic.
   In this case, the proof should be terminated with :cmd:`Defined` in order to define a :term:`constant`
   for which the computational behavior is relevant.  See :ref:`proof-editing-mode`.

   This command accepts the :attr:`using` attribute.

   .. note::

      + Some fixpoints may have several arguments that fit as decreasing
        arguments, and this choice influences the reduction of the fixpoint.
        Hence an explicit annotation must be used if the leftmost decreasing
        argument is not the desired one. Writing explicit annotations can also
        speed up type checking of large mutual fixpoints.

      + In order to keep the strong normalization property, the fixed point
        reduction will only be performed when the argument in position of the
        decreasing argument (which type should be in an inductive definition)
        starts with a constructor.


   .. example::

      One can define the addition function as :

      .. rocqtop:: all

         Fixpoint add (n m:nat) {struct n} : nat :=
         match n with
         | O => m
         | S p => S (add p m)
         end.

      The match operator matches a value (here :g:`n`) with the various
      constructors of its (inductive) type. The remaining arguments give the
      respective values to be returned, as functions of the parameters of the
      corresponding constructor. Thus here when :g:`n` equals :g:`O` we return
      :g:`m`, and when :g:`n` equals :g:`(S p)` we return :g:`(S (add p m))`.

      The match operator is formally described in
      Section :ref:`match-construction`.
      The system recognizes that in the inductive call :g:`(add p m)` the first
      argument actually decreases because it is a *pattern variable* coming
      from :g:`match n with`.

   .. example::

      The following definition is not correct and generates an error message:

      .. rocqtop:: all

         Fail Fixpoint wrongplus (n m:nat) {struct n} : nat :=
         match m with
         | O => n
         | S p => S (wrongplus n p)
         end.

      because the declared decreasing argument :g:`n` does not actually
      decrease in the recursive call.

      .. _reversed_add_example:

      The function computing the addition over the second argument should rather be written:

      .. rocqtop:: all

         Fixpoint plus (n m:nat) {struct m} : nat :=
         match m with
         | O => n
         | S p => S (plus n p)
         end.

      **Aside**: Observe that `plus n 0` is reducible but `plus 0 n` is not,
      the reverse of `Nat.add`, for which `0 + n` is reducible and `n + 0` is not.

      .. rocqtop:: all

         Goal forall n:nat, plus n 0 = plus 0 n.
         Proof.
         intros; simpl.  (* plus 0 n not reducible *)

      .. rocqtop:: none

         Abort.

      .. rocqtop:: all

         Goal forall n:nat, n + 0 = 0 + n.
         Proof.
         intros; simpl.  (* n + 0 not reducible *)

      .. rocqtop:: none

         Abort.

   .. example::

      The recursive call may not only be on direct subterms of the recursive
      variable :g:`n` but also on a deeper subterm and we can directly write
      the function :g:`mod2` which gives the remainder modulo 2 of a natural
      number.

      .. rocqtop:: all

         Fixpoint mod2 (n:nat) : nat :=
         match n with
         | O => O
         | S p => match p with
                  | O => S O
                  | S q => mod2 q
                  end
         end.

.. _example_mutual_fixpoints:

   .. example:: Mutual fixpoints

      The size of trees and forests can be defined the following way:

      .. rocqtop:: all

         Fixpoint tree_size (t:tree) : nat :=
         match t with
         | node a f => S (forest_size f)
         end
         with forest_size (f:forest) : nat :=
         match f with
         | leaf b => 1
         | cons t f' => (tree_size t + forest_size f')
         end.

.. extracted from CIC chapter

.. _inductive-definitions:

Theory of inductive definitions
-------------------------------

Formally, we can represent any *inductive definition* as
:math:`\ind{p}{Γ_I}{Γ_C}` where:

+ :math:`Γ_I` determines the names and types of inductive types;
+ :math:`Γ_C` determines the names and types of constructors of these
  inductive types;
+ :math:`p` determines the number of parameters of these inductive types.


These inductive definitions, together with global assumptions and
global definitions, then form the global environment. Additionally,
for any :math:`p` there always exists :math:`Γ_P =[a_1 :A_1 ;~…;~a_p :A_p ]` such that
each :math:`T` in :math:`(t:T)∈Γ_I \cup Γ_C` can be written as: :math:`∀Γ_P , T'` where :math:`Γ_P` is
called the *context of parameters*. Furthermore, we must have that
each :math:`T` in :math:`(t:T)∈Γ_I` can be written as: :math:`∀Γ_P,∀Γ_{\mathit{Arr}(t)}, S` where
:math:`Γ_{\mathit{Arr}(t)}` is called the *Arity* of the inductive type :math:`t` and :math:`S` is called
the sort of the inductive type :math:`t` (not to be confused with :math:`\Sort` which is the set of sorts).

.. example::

   The declaration for parameterized lists is:

   .. math::
      \ind{1}{[\List:\Set→\Set]}{\left[\begin{array}{rcl}
      \Nil & : & ∀ A:\Set,~\List~A \\
      \cons & : & ∀ A:\Set,~A→ \List~A→ \List~A
      \end{array}
      \right]}

   which corresponds to the result of the Rocq declaration:

   .. rocqtop:: in reset

      Inductive list (A:Set) : Set :=
      | nil : list A
      | cons : A -> list A -> list A.

.. example::

   The declaration for a mutual inductive definition of tree and forest
   is:

   .. math::
      \ind{0}{\left[\begin{array}{rcl}\tree&:&\Set\\\forest&:&\Set\end{array}\right]}
       {\left[\begin{array}{rcl}
                \node &:& \forest → \tree\\
                \emptyf &:& \forest\\
                \consf &:& \tree → \forest → \forest\\
                          \end{array}\right]}

   which corresponds to the result of the Rocq declaration:

   .. rocqtop:: in

      Inductive tree : Set :=
      | node : forest -> tree
      with forest : Set :=
      | emptyf : forest
      | consf : tree -> forest -> forest.

.. example::

   The declaration for a mutual inductive definition of even and odd is:

   .. math::
      \ind{0}{\left[\begin{array}{rcl}\even&:&\nat → \Prop \\
                                      \odd&:&\nat → \Prop \end{array}\right]}
       {\left[\begin{array}{rcl}
                \evenO &:& \even~0\\
                \evenS &:& ∀ n,~\odd~n → \even~(\nS~n)\\
                \oddS &:& ∀ n,~\even~n → \odd~(\nS~n)
                          \end{array}\right]}

   which corresponds to the result of the Rocq declaration:

   .. rocqtop:: in

      Inductive even : nat -> Prop :=
      | even_O : even 0
      | even_S : forall n, odd n -> even (S n)
      with odd : nat -> Prop :=
      | odd_S : forall n, even n -> odd (S n).



.. _Types-of-inductive-objects:

Types of inductive objects
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

We have to give the type of constants in a global environment :math:`E` which
contains an inductive definition.

.. inference:: Ind

   \WFE{Γ}
   \ind{p}{Γ_I}{Γ_C} ∈ E
   (a:A)∈Γ_I
   ---------------------
   E[Γ] ⊢ a : A

.. inference:: Constr

   \WFE{Γ}
   \ind{p}{Γ_I}{Γ_C} ∈ E
   (c:C)∈Γ_C
   ---------------------
   E[Γ] ⊢ c : C

.. example::

   Provided that our global environment :math:`E` contains inductive definitions we showed before,
   these two inference rules above enable us to conclude that:

   .. math::
      \begin{array}{l}
      E[Γ] ⊢ \even : \nat→\Prop\\
      E[Γ] ⊢ \odd : \nat→\Prop\\
      E[Γ] ⊢ \evenO : \even~\nO\\
      E[Γ] ⊢ \evenS : ∀ n:\nat,~\odd~n → \even~(\nS~n)\\
      E[Γ] ⊢ \oddS : ∀ n:\nat,~\even~n → \odd~(\nS~n)
      \end{array}




.. _Well-formed-inductive-definitions:

Well-formed inductive definitions
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

We cannot accept any inductive definition because some of them lead
to inconsistent systems. We restrict ourselves to definitions which
satisfy a syntactic criterion of positivity. Before giving the formal
rules, we need a few definitions:

Arity of a given sort
+++++++++++++++++++++

A type :math:`T` is an *arity of sort* :math:`s` if it converts to the sort :math:`s` or to a
product :math:`∀ x:T,~U` with :math:`U` an arity of sort :math:`s`.

.. example::

   :math:`A→\Set` is an arity of sort :math:`\Set`. :math:`∀ A:\Prop,~A→ \Prop` is an arity of sort
   :math:`\Prop`.


Arity
+++++
A type :math:`T` is an *arity* if there is a :math:`s∈ \Sort` such that :math:`T` is an arity of
sort :math:`s`.


.. example::

   :math:`A→ \Set` and :math:`∀ A:\Prop,~A→ \Prop` are arities.

..
   Convention in describing inductive types:
   k is the number of inductive types (I_i : forall params, A_i)
   n is the number of constructors in the whole block (c_i : forall params, C_i)
   r is the number of parameters
   l is the size of the context of parameters (p_i : P_i)
   m is the number of recursively non-uniform parameters among parameters
   s is the number of indices
   q = r+s is the number of parameters and indices


Type of constructor
+++++++++++++++++++
We say that :math:`T` is a *type of constructor of* :math:`I` in one of the following
two cases:

+ :math:`T` is :math:`(I~t_1 … t_q )`
+ :math:`T` is :math:`∀ x:U,~T'` where :math:`T'` is also a type of constructor of :math:`I`

.. example::

   :math:`\nat` and :math:`\nat→\nat` are types of constructor of :math:`\nat`.
   :math:`∀ A:\Type,~\List~A` and :math:`∀ A:\Type,~A→\List~A→\List~A` are types of constructor of :math:`\List`.

.. _positivity:

Positivity Condition
++++++++++++++++++++

The type of constructor :math:`T` will be said to *satisfy the positivity
condition* for a set of constants :math:`X_1 … X_k` in the following cases:

+ :math:`T=(X_j~t_1 … t_q )` for some :math:`j` and no :math:`X_1 … X_k` occur free in any :math:`t_i`
+ :math:`T=∀ x:U,~V` and :math:`X_1 … X_k` occur only strictly positively in :math:`U` and the type :math:`V`
  satisfies the positivity condition for :math:`X_1 … X_k`.

Strict positivity
+++++++++++++++++

The constants :math:`X_1 … X_k` *occur strictly positively* in :math:`T` in the following
cases:


+ no :math:`X_1 … X_k` occur in :math:`T`
+ :math:`T` converts to :math:`(X_j~t_1 … t_q )` for some :math:`j` and no :math:`X_1 … X_k` occur in any of :math:`t_i`
+ :math:`T` converts to :math:`∀ x:U,~V` and :math:`X_1 … X_k` occur
  strictly positively in type :math:`V` but none of them occur in :math:`U`
+ :math:`T` converts to :math:`(I~a_1 … a_r~t_1 … t_s )` where :math:`I` is the name of an
  inductive definition of the form

  .. math::
     \ind{r}{I:A}{c_1 :∀ p_1 :P_1 ,… ∀p_r :P_r ,~C_1 ;~…;~c_n :∀ p_1 :P_1 ,… ∀p_r :P_r ,~C_n}

  (in particular, it is
  not mutually defined and it has :math:`r` parameters) and no :math:`X_1 … X_k` occur in
  any of the :math:`t_i` nor in any of the :math:`a_j` for :math:`m < j ≤ r` where :math:`m ≤ r`
  is the number of recursively uniform parameters, and the (instantiated) types of constructor
  :math:`\subst{C_i}{p_j}{a_j}_{j=1… m}` of :math:`I` satisfy the nested positivity condition for :math:`X_1 … X_k`

.. _nested-positivity:

Nested Positivity
+++++++++++++++++

If :math:`I` is a non-mutual inductive type with :math:`r`
parameters, then,
the type of constructor :math:`T` of :math:`I` *satisfies the nested
positivity condition* for a set of constants :math:`X_1 … X_k` in the following
cases:

+ :math:`T=(I~b_1 … b_r~u_1 … u_s)` and no :math:`X_1 … X_k` occur in
  any :math:`u_i` nor in
  any of the :math:`b_j` for :math:`m < j ≤ r` where :math:`m ≤ r` is
  the number of recursively uniform parameters

+ :math:`T=∀ x:U,~V` and :math:`X_1 … X_k` occur only strictly positively in :math:`U` and the type :math:`V`
  satisfies the nested positivity condition for :math:`X_1 … X_k`


.. example::

   For instance, if one considers the following variant of a tree type
   branching over the natural numbers:

   .. rocqtop:: in

      Inductive nattree (A:Type) : Type :=
      | leaf : nattree A
      | natnode : A -> (nat -> nattree A) -> nattree A.

   Then every instantiated constructor of ``nattree A`` satisfies the nested positivity
   condition for ``nattree``:

   + Type ``nattree A`` of constructor ``leaf`` satisfies the positivity condition for
     ``nattree`` because ``nattree`` does not appear in any (real) arguments of the
     type of that constructor (primarily because ``nattree`` does not have any (real)
     arguments) ... (bullet 1)

   + Type ``A → (nat → nattree A) → nattree A`` of constructor ``natnode`` satisfies the
     positivity condition for ``nattree`` because:

     - ``nattree`` occurs only strictly positively in ``A`` ... (bullet 1)

     - ``nattree`` occurs only strictly positively in ``nat → nattree A`` ... (bullet 3 + 2)

     - ``nattree`` satisfies the positivity condition for ``nattree A`` ... (bullet 1)

.. _Correctness-rules:

Correctness rules
+++++++++++++++++

We shall now describe the rules allowing the introduction of a new
inductive definition.

Let :math:`E` be a global environment and :math:`Γ_P`, :math:`Γ_I`, :math:`Γ_C` be contexts
such that :math:`Γ_I` is :math:`[I_1 :∀ Γ_P ,A_1 ;~…;~I_k :∀ Γ_P ,A_k]`, and
:math:`Γ_C` is :math:`[c_1:∀ Γ_P ,C_1 ;~…;~c_n :∀ Γ_P ,C_n ]`. Then

.. inference:: W-Ind

   \WFE{Γ_P}
   (E[Γ_I ;Γ_P ] ⊢ C_i : s_{q_i} )_{i=1… n}
   ------------------------------------------
   \WF{E;~\ind{l}{Γ_I}{Γ_C}}{}


provided that the following side conditions hold:

    + :math:`k>0` and all of :math:`I_j` and :math:`c_i` are distinct names for :math:`j=1… k` and :math:`i=1… n`,
    + :math:`l` is the size of :math:`Γ_P` which is called the context of parameters,
    + for :math:`j=1… k` we have that :math:`A_j` is an arity of sort :math:`s_j` and :math:`I_j ∉ E`,
    + for :math:`i=1… n` we have that :math:`C_i` is a type of constructor of :math:`I_{q_i}` which
      satisfies the positivity condition for :math:`I_1 … I_k` and :math:`c_i ∉  E`.

Additionally, for :math:`j=1… k` the following universe constraints must be satisfied,
or :math:`s_j` must be an impredicative sort (`SProp`, `Prop`, or if `-impredicative-set` was used `Set`)
and the `j`\th inductive may not be eliminated to larger sorts:

- for each (non parameter) constructor argument, the universe of its type must be smaller than :math:`s_j`
- if ``-indices-matter`` or :flag:`Indices Matter` was used, for each index the universe of its type must be smaller than :math:`s_j`.
  When neither ``-indices-matter`` nor :flag:`Indices Matter` is used, inductives whose indices would contribute
  universe constraints are printed by :cmd:`Print Assumptions`.
- if there are 2 or more constructors, `Set` must be smaller than :math:`s_j`
- unless the inductive is a primitive record, and unless :flag:`Definitional UIP` was used,
  if there is 1 constructor, `Prop` must be smaller than :math:`s_j` (essentially this means :math:`s_j` must not be `SProp`)

.. example::

   It is well known that the existential quantifier can be encoded as an
   inductive definition. The following declaration introduces the
   second-order existential quantifier :math:`∃ X.P(X)`.

   .. rocqtop:: in

      Inductive exProp (P:Prop->Prop) : Prop :=
      | exP_intro : forall X:Prop, P X -> exProp P.

   The same definition on :math:`\Set` is not allowed and fails:

   .. rocqtop:: all

      Fail Inductive exSet (P:Set->Prop) : Set :=
      exS_intro : forall X:Set, P X -> exSet P.

   It is possible to declare the same inductive definition in the
   universe :math:`\Type`. The :g:`exType` inductive definition has type
   :math:`(\Type(i)→\Prop)→\Type(j)` with the constraint that the parameter :math:`X` of :math:`\kw{exT}_{\kw{intro}}`
   has type :math:`\Type(k)` with :math:`k<j` and :math:`k≤ i`.

   .. rocqtop:: all

      Inductive exType (P:Type->Prop) : Type :=
      exT_intro : forall X:Type, P X -> exType P.


.. example:: Negative occurrence (first example)

   The following inductive definition is rejected because it does not
   satisfy the positivity condition:

   .. rocqtop:: all

      Fail Inductive I : Prop := not_I_I (not_I : I -> False) : I.

   If we were to accept such definition, we could derive a
   contradiction from it (we can test this by disabling the
   :flag:`Positivity Checking` flag):

   .. rocqtop:: in

      #[bypass_check(positivity)] Inductive I : Prop := not_I_I (not_I : I -> False) : I.

   .. rocqtop:: all

      Definition I_not_I : I -> ~ I := fun i =>
        match i with not_I_I not_I => not_I end.

   .. rocqtop:: in

      Lemma contradiction : False.
      Proof.
        enough (I /\ ~ I) as [] by contradiction.
        split.
        - apply not_I_I.
          intro.
          now apply I_not_I.
        - intro.
          now apply I_not_I.
      Qed.

.. example:: Negative occurrence (second example)

   Here is another example of an inductive definition which is
   rejected because it does not satify the positivity condition:

   .. rocqtop:: all

      Fail Inductive Lam := lam (_ : Lam -> Lam).

   Again, if we were to accept it, we could derive a contradiction
   (this time through a non-terminating recursive function):

   .. rocqtop:: in

      #[bypass_check(positivity)] Inductive Lam := lam (_ : Lam -> Lam).

   .. rocqtop:: all

      Fixpoint infinite_loop l : False :=
        match l with lam x => infinite_loop (x l) end.

      Check infinite_loop (lam (@id Lam)) : False.

.. example:: Non strictly positive occurrence

   It is less obvious why inductive type definitions with occurences
   that are positive but not strictly positive are harmful.
   We will see that in presence of an impredicative type they
   are unsound:

   .. rocqtop:: all

      Fail Inductive A: Type := introA: ((A -> Prop) -> Prop) -> A.

   If we were to accept this definition we could derive a contradiction
   by creating an injective function from :math:`A → \Prop` to :math:`A`.

   This function is defined by composing the injective constructor of
   the type :math:`A` with the function :math:`λx. λz. z = x` injecting
   any type :math:`T` into :math:`T → \Prop`.

   .. rocqtop:: in

      #[bypass_check(positivity)] Inductive A: Type := introA: ((A -> Prop) -> Prop) -> A.

   .. rocqtop:: all

      Definition f (x: A -> Prop): A := introA (fun z => z = x).

   .. rocqtop:: in

      Lemma f_inj: forall x y, f x = f y -> x = y.
      Proof.
        unfold f; intros ? ? H; injection H.
        set (F := fun z => z = y); intro HF.
        symmetry; replace (y = x) with (F y).
        + unfold F; reflexivity.
        + rewrite <- HF; reflexivity.
      Qed.

   The type :math:`A → \Prop` can be understood as the powerset
   of the type :math:`A`. To derive a contradiction from the
   injective function :math:`f` we use Cantor's classic diagonal
   argument.

   .. rocqtop:: all

      Definition d: A -> Prop := fun x => exists s, x = f s /\ ~s x.
      Definition fd: A := f d.

   .. rocqtop:: in

      Lemma cantor: (d fd) <-> ~(d fd).
      Proof.
        split.
        + intros [s [H1 H2]]; unfold fd in H1.
          replace d with s.
          * assumption.
          * apply f_inj; congruence.
        + intro; exists d; tauto.
      Qed.

      Lemma bad: False.
      Proof.
        pose cantor; tauto.
      Qed.

   This derivation was first presented by Thierry Coquand and Christine
   Paulin in :cite:`CP90`.

.. _Destructors:

Destructors
~~~~~~~~~~~~~~~~~

The specification of inductive definitions with arities and
constructors is quite natural. But we still have to say how to use an
object in an inductive type.

This problem is rather delicate. There are actually several different
ways to do that. Some of them are logically equivalent but not always
equivalent from the computational point of view or from the user point
of view.

From the computational point of view, we want to be able to define a
function whose domain is an inductively defined type by using a
combination of case analysis over the possible constructors of the
object and recursion.

Because we need to keep a consistent theory and also we prefer to keep
a strongly normalizing reduction, we cannot accept any sort of
recursion (even terminating). So the basic idea is to restrict
ourselves to primitive recursive functions and functionals.

For instance, assuming a parameter :math:`A:\Set` exists in the local context,
we want to build a function :math:`\length` of type :math:`\List~A → \nat` which computes
the length of the list, such that :math:`(\length~(\Nil~A)) = \nO` and
:math:`(\length~(\cons~A~a~l)) = (\nS~(\length~l))`.
We want these equalities to be
recognized implicitly and taken into account in the conversion rule.

From the logical point of view, we have built a type family by giving
a set of constructors. We want to capture the fact that we do not have
any other way to build an object in this type. So when trying to prove
a property about an object :math:`m` in an inductive type it is enough
to enumerate all the cases where :math:`m` starts with a different
constructor.

In case the inductive definition is effectively a recursive one, we
want to capture the extra property that we have built the smallest
fixed point of this recursive equation. This says that we are only
manipulating finite objects. This analysis provides induction
principles. For instance, in order to prove
:math:`∀ l:\List~A,~(\kw{has}\_\kw{length}~A~l~(\length~l))` it is enough to prove:


+ :math:`(\kw{has}\_\kw{length}~A~(\Nil~A)~(\length~(\Nil~A)))`
+ :math:`∀ a:A,~∀ l:\List~A,~(\kw{has}\_\kw{length}~A~l~(\length~l)) →`
  :math:`(\kw{has}\_\kw{length}~A~(\cons~A~a~l)~(\length~(\cons~A~a~l)))`


which given the conversion equalities satisfied by :math:`\length` is the same
as proving:


+ :math:`(\kw{has}\_\kw{length}~A~(\Nil~A)~\nO)`
+ :math:`∀ a:A,~∀ l:\List~A,~(\kw{has}\_\kw{length}~A~l~(\length~l)) →`
  :math:`(\kw{has}\_\kw{length}~A~(\cons~A~a~l)~(\nS~(\length~l)))`


One conceptually simple way to do that, following the basic scheme
proposed by Martin-Löf in his Intuitionistic Type Theory, is to
introduce for each inductive definition an elimination operator. At
the logical level it is a proof of the usual induction principle and
at the computational level it implements a generic operator for doing
primitive recursion over the structure.

But this operator is rather tedious to implement and use. We choose
to factorize the operator for primitive recursion
into two more primitive operations as was first suggested by Th.
Coquand in :cite:`Coq92`. One is the definition by pattern matching. The
second one is a definition by guarded fixpoints.


.. _match-construction:

The match ... with ... end construction
+++++++++++++++++++++++++++++++++++++++

The basic idea of this operator is that we have an object :math:`m` in an
inductive type :math:`I` and we want to prove a property which possibly
depends on :math:`m`. For this, it is enough to prove the property for
:math:`m = (c_i~u_1 … u_{p_i} )` for each constructor of :math:`I`.
The Rocq term for this proof
will be written:

.. math::
   \Match~m~\with~(c_1~x_{11} ... x_{1p_1} ) ⇒ f_1 | … | (c_n~x_{n1} ... x_{np_n} ) ⇒ f_n~\kwend

In this expression, if :math:`m` eventually happens to evaluate to
:math:`(c_i~u_1 … u_{p_i})` then the expression will behave as specified in its :math:`i`-th branch
and it will reduce to :math:`f_i` where the :math:`x_{i1} …x_{ip_i}` are replaced by the
:math:`u_1 … u_{p_i}` according to the ι-reduction.

Actually, for type checking a :math:`\Match…\with…\kwend` expression we also need
to know the predicate :math:`P` to be proved by case analysis. In the general
case where :math:`I` is an inductively defined :math:`n`-ary relation, :math:`P` is a predicate
over :math:`n+1` arguments: the :math:`n` first ones correspond to the arguments of :math:`I`
(parameters excluded), and the last one corresponds to object :math:`m`. Rocq
can sometimes infer this predicate but sometimes not. The concrete
syntax for describing this predicate uses the :math:`\as…\In…\return`
construction. For instance, let us assume that :math:`I` is an unary predicate
with one parameter and one argument. The predicate is made explicit
using the syntax:

.. math::
   \Match~m~\as~x~\In~I~\_~a~\return~P~\with~
   (c_1~x_{11} ... x_{1p_1} ) ⇒ f_1 | …
   | (c_n~x_{n1} ... x_{np_n} ) ⇒ f_n~\kwend

The :math:`\as` part can be omitted if either the result type does not depend
on :math:`m` (non-dependent elimination) or :math:`m` is a variable (in this case, :math:`m`
can occur in :math:`P` where it is considered a bound variable). The :math:`\In` part
can be omitted if the result type does not depend on the arguments
of :math:`I`. Note that the arguments of :math:`I` corresponding to parameters *must*
be :math:`\_`, because the result type is not generalized to all possible
values of the parameters. The other arguments of :math:`I` (sometimes called
indices in the literature) have to be variables (:math:`a` above) and these
variables can occur in :math:`P`. The expression after :math:`\In` must be seen as an
*inductive type pattern*. Notice that expansion of implicit arguments
and notations apply to this pattern. For the purpose of presenting the
inference rules, we use a more compact notation:

.. math::
   \case(m,(λ a x . P), λ x_{11} ... x_{1p_1} . f_1~| … |~λ x_{n1} ...x_{np_n} . f_n )


.. _Allowed-elimination-sorts:

**Allowed elimination sorts.** An important question for building the typing rule for :math:`\Match` is what
can be the type of :math:`λ a x . P` with respect to the type of :math:`m`. If :math:`m:I`
and :math:`I:A` and :math:`λ a x . P : B` then by :math:`[I:A|B]` we mean that one can use
:math:`λ a x . P` with :math:`m` in the above match-construct.


.. _cic_notations:

**Notations.** The :math:`[I:A|B]` is defined as the smallest relation satisfying the
following rules: We write :math:`[I|B]` for :math:`[I:A|B]` where :math:`A` is the type of :math:`I`.

The case of inductive types in sorts :math:`\Set` or :math:`\Type` is simple.
There is no restriction on the sort of the predicate to be eliminated.

.. inference:: Prod

   [(I~x):A′|B′]
   -----------------------
   [I:∀ x:A,~A′|∀ x:A,~B′]


.. inference:: Set & Type

   s_1 ∈ \{\Set,\Type(j)\}
   s_2 ∈ \Sort
   ----------------
   [I:s_1 |I→ s_2 ]


The case of Inductive definitions of sort :math:`\Prop` is a bit more
complicated, because of our interpretation of this sort. The only
harmless allowed eliminations, are the ones when predicate :math:`P`
is also of sort :math:`\Prop` or is of the morally smaller sort
:math:`\SProp`.

.. inference:: Prop

   s ∈ \{\SProp,\Prop\}
   --------------------
   [I:\Prop|I→s]


:math:`\Prop` is the type of logical propositions, the proofs of properties :math:`P` in
:math:`\Prop` could not be used for computation and are consequently ignored by
the extraction mechanism. Assume :math:`A` and :math:`B` are two propositions, and the
logical disjunction :math:`A ∨ B` is defined inductively by:

.. example::

   .. rocqtop:: in

      Inductive or (A B:Prop) : Prop :=
      or_introl : A -> or A B | or_intror : B -> or A B.


The following definition which computes a boolean value by case over
the proof of :g:`or A B` is not accepted:

.. example::

   .. rocqtop:: all

      Fail Definition choice (A B: Prop) (x:or A B) :=
      match x with or_introl _ _ a => true | or_intror _ _ b => false end.

From the computational point of view, the structure of the proof of
:g:`(or A B)` in this term is needed for computing the boolean value.

In general, if :math:`I` has type :math:`\Prop` then :math:`P` cannot have type :math:`I→\Set`, because
it will mean to build an informative proof of type :math:`(P~m)` doing a case
analysis over a non-computational object that will disappear in the
extracted program. But the other way is safe with respect to our
interpretation we can have :math:`I` a computational object and :math:`P` a
non-computational one, it just corresponds to proving a logical property
of a computational object.

In the same spirit, elimination on :math:`P` of type :math:`I→\Type` cannot be allowed
because it trivially implies the elimination on :math:`P` of type :math:`I→ \Set` by
cumulativity. It also implies that there are two proofs of the same
property which are provably different, contradicting the
proof-irrelevance property which is sometimes a useful axiom:

.. example::

   .. rocqtop:: all

      Axiom proof_irrelevance : forall (P : Prop) (x y : P), x=y.

The elimination of an inductive type of sort :math:`\Prop` on a predicate
:math:`P` of type :math:`I→ \Type` leads to a paradox when applied to impredicative
inductive definition like the second-order existential quantifier
:g:`exProp` defined above, because it gives access to the two projections on
this type.


.. _Empty-and-singleton-elimination:

**Empty and singleton elimination.** There are special inductive definitions in
:math:`\Prop` for which more eliminations are allowed.

.. inference:: Prop-extended

   I~\kw{is an empty or singleton definition}
   s ∈ \Sort
   -------------------------------------
   [I:\Prop|I→ s]

A *singleton definition* has only one constructor and all the
arguments of this constructor have type :math:`\Prop`. In that case, there is a
canonical way to interpret the informative extraction on an object in
that type, such that the elimination on any sort :math:`s` is legal. Typical
examples are the conjunction of non-informative propositions and the
equality. If there is a hypothesis :math:`h:a=b` in the local context, it can
be used for rewriting not only in logical propositions but also in any
type.

.. example::

   .. rocqtop:: all

      Print eq_rec.
      Require Extraction.
      Extraction eq_rec.

An empty definition has no constructors, in that case also,
elimination on any sort is allowed.

.. _Eliminaton-for-SProp:

Inductive types in :math:`\SProp` must have no constructors (i.e. be
empty) to be eliminated to produce relevant values.

Note that thanks to proof irrelevance elimination functions can be
produced for other types, for instance the elimination for a unit type
is the identity.

.. _Type-of-branches:

**Type of branches.**
Let :math:`c` be a term of type :math:`C`, we assume :math:`C` is a type of constructor for an
inductive type :math:`I`. Let :math:`P` be a term that represents the property to be
proved. We assume :math:`r` is the number of parameters and :math:`s` is the number of
arguments.

We define a new type :math:`\{c:C\}^P` which represents the type of the branch
corresponding to the :math:`c:C` constructor.

.. math::
   \begin{array}{ll}
   \{c:(I~q_1\ldots q_r\ t_1 \ldots t_s)\}^P &\equiv (P~t_1\ldots ~t_s~c) \\
   \{c:∀ x:T,~C\}^P &\equiv ∀ x:T,~\{(c~x):C\}^P
   \end{array}

We write :math:`\{c\}^P` for :math:`\{c:C\}^P` with :math:`C` the type of :math:`c`.


.. example::

   The following term in concrete syntax::

       match t as l return P' with
       | nil _ => t1
       | cons _ hd tl => t2
       end


   can be represented in abstract syntax as

   .. math::
      \case(t,P,f_1 | f_2 )

   where

   .. math::
      :nowrap:

      \begin{eqnarray*}
        P & = & λ l.~P^\prime\\
        f_1 & = & t_1\\
        f_2 & = & λ (hd:\nat).~λ (tl:\List~\nat).~t_2
      \end{eqnarray*}

   According to the definition:

   .. math::
      \{(\Nil~\nat)\}^P ≡ \{(\Nil~\nat) : (\List~\nat)\}^P ≡ (P~(\Nil~\nat))

   .. math::

      \begin{array}{rl}
      \{(\cons~\nat)\}^P & ≡\{(\cons~\nat) : (\nat→\List~\nat→\List~\nat)\}^P \\
      & ≡∀ n:\nat,~\{(\cons~\nat~n) : (\List~\nat→\List~\nat)\}^P \\
      & ≡∀ n:\nat,~∀ l:\List~\nat,~\{(\cons~\nat~n~l) : (\List~\nat)\}^P \\
      & ≡∀ n:\nat,~∀ l:\List~\nat,~(P~(\cons~\nat~n~l)).
      \end{array}

   Given some :math:`P` then :math:`\{(\Nil~\nat)\}^P` represents the expected type of :math:`f_1`,
   and :math:`\{(\cons~\nat)\}^P` represents the expected type of :math:`f_2`.


.. _Typing-rule:

**Typing rule.**
Our very general destructor for inductive definitions has the
following typing rule

.. inference:: match

   \begin{array}{l}
   E[Γ] ⊢ c : (I~q_1 … q_r~t_1 … t_s ) \\
   E[Γ] ⊢ P : B \\
   [(I~q_1 … q_r)|B] \\
   (E[Γ] ⊢ f_i : \{(c_{p_i}~q_1 … q_r)\}^P)_{i=1… l}
   \end{array}
   ------------------------------------------------
   E[Γ] ⊢ \case(c,P,f_1  |… |f_l ) : (P~t_1 … t_s~c)

provided :math:`I` is an inductive type in a
definition :math:`\ind{r}{Γ_I}{Γ_C}` with :math:`Γ_C = [c_1 :C_1 ;~…;~c_n :C_n ]` and
:math:`c_{p_1} … c_{p_l}` are the only constructors of :math:`I`.



.. example::

   Below is a typing rule for the term shown in the previous example:

   .. inference:: list example

     \begin{array}{l}
       E[Γ] ⊢ t : (\List ~\nat) \\
       E[Γ] ⊢ P : B \\
       [(\List ~\nat)|B] \\
       E[Γ] ⊢ f_1 : \{(\Nil ~\nat)\}^P \\
       E[Γ] ⊢ f_2 : \{(\cons ~\nat)\}^P
     \end{array}
     ------------------------------------------------
     E[Γ] ⊢ \case(t,P,f_1 |f_2 ) : (P~t)


.. _Definition-of-ι-reduction:

**Definition of ι-reduction.**
We still have to define the ι-reduction in the general case.

An ι-redex is a term of the following form:

.. math::
   \case((c_{p_i}~q_1 … q_r~a_1 … a_m ),P,f_1 |… |f_l )

with :math:`c_{p_i}` the :math:`i`-th constructor of the inductive type :math:`I` with :math:`r`
parameters.

The ι-contraction of this term is :math:`(f_i~a_1 … a_m )` leading to the
general reduction rule:

.. math::
   \case((c_{p_i}~q_1 … q_r~a_1 … a_m ),P,f_1 |… |f_l ) \triangleright_ι (f_i~a_1 … a_m )


.. _Fixpoint-definitions:

Fixpoint definitions
~~~~~~~~~~~~~~~~~~~~

The second operator for elimination is fixpoint definition. This
fixpoint may involve several mutually recursive definitions. The basic
concrete syntax for a recursive set of mutually recursive declarations
is (with :math:`Γ_i` contexts):

.. math::
   \fix~f_1 (Γ_1 ) :A_1 :=t_1~\with … \with~f_n (Γ_n ) :A_n :=t_n


The terms are obtained by projections from this set of declarations
and are written

.. math::
   \fix~f_1 (Γ_1 ) :A_1 :=t_1~\with … \with~f_n (Γ_n ) :A_n :=t_n~\for~f_i

In the inference rules, we represent such a term by

.. math::
   \Fix~f_i\{f_1 :A_1':=t_1' … f_n :A_n':=t_n'\}

with :math:`t_i'` (resp. :math:`A_i'`) representing the term :math:`t_i` abstracted (resp.
generalized) with respect to the bindings in the context :math:`Γ_i`, namely
:math:`t_i'=λ Γ_i . t_i` and :math:`A_i'=∀ Γ_i , A_i`.


Typing rule
+++++++++++

The typing rule is the expected one for a fixpoint.

.. inference:: Fix

   (E[Γ] ⊢ A_i : s_i )_{i=1… n}
   (E[Γ;~f_1 :A_1 ;~…;~f_n :A_n ] ⊢ t_i : A_i )_{i=1… n}
   -------------------------------------------------------
   E[Γ] ⊢ \Fix~f_i\{f_1 :A_1 :=t_1 … f_n :A_n :=t_n \} : A_i


Any fixpoint definition cannot be accepted because non-normalizing
terms allow proofs of absurdity. The basic scheme of recursion that
should be allowed is the one needed for defining primitive recursive
functionals. In that case the fixpoint enjoys a special syntactic
restriction, namely one of the arguments belongs to an inductive type,
the function starts with a case analysis and recursive calls are done
on variables coming from patterns and representing subterms. For
instance in the case of natural numbers, a proof of the induction
principle of type

.. math::
   ∀ P:\nat→\Prop,~(P~\nO)→(∀ n:\nat,~(P~n)→(P~(\nS~n)))→ ∀ n:\nat,~(P~n)

can be represented by the term:

.. math::
   \begin{array}{l}
   λ P:\nat→\Prop.~λ f:(P~\nO).~λ g:(∀ n:\nat,~(P~n)→(P~(\nS~n))).\\
   \Fix~h\{h:∀ n:\nat,~(P~n):=λ n:\nat.~\case(n,P,f | λp:\nat.~(g~p~(h~p)))\}
   \end{array}

Before accepting a fixpoint definition as being correctly typed, we
check that the definition is “guarded”. A precise analysis of this
notion can be found in :cite:`Gim94`. The first stage is to precise on which
argument the fixpoint will be decreasing. The type of this argument
should be an inductive type. For doing this, the syntax of
fixpoints is extended and becomes

.. math::
   \Fix~f_i\{f_1/k_1 :A_1:=t_1 … f_n/k_n :A_n:=t_n\}


where :math:`k_i` are positive integers. Each :math:`k_i` represents the index of
parameter of :math:`f_i`, on which :math:`f_i` is decreasing. Each :math:`A_i` should be a
type (reducible to a term) starting with at least :math:`k_i` products
:math:`∀ y_1 :B_1 ,~… ∀ y_{k_i} :B_{k_i} ,~A_i'` and :math:`B_{k_i}` an inductive type.

Now in the definition :math:`t_i`, if :math:`f_j` occurs then it should be applied to
at least :math:`k_j` arguments and the :math:`k_j`-th argument should be
syntactically recognized as structurally smaller than :math:`y_{k_i}`.

The definition of being structurally smaller is a bit technical. One
needs first to define the notion of *recursive arguments of a
constructor*. For an inductive definition :math:`\ind{r}{Γ_I}{Γ_C}`, if the
type of a constructor :math:`c` has the form
:math:`∀ p_1 :P_1 ,~… ∀ p_r :P_r,~∀ x_1:T_1,~… ∀ x_m :T_m,~(I_j~p_1 … p_r~t_1 … t_s )`,
then the recursive
arguments will correspond to :math:`T_i` in which one of the :math:`I_l` occurs.

The main rules for being structurally smaller are the following.
Given a variable :math:`y` of an inductively defined type in a declaration
:math:`\ind{r}{Γ_I}{Γ_C}` where :math:`Γ_I` is :math:`[I_1 :A_1 ;~…;~I_k :A_k]`, and :math:`Γ_C` is
:math:`[c_1 :C_1 ;~…;~c_n :C_n ]`, the terms structurally smaller than :math:`y` are:


+ :math:`(t~u)` and :math:`λ x:U .~t` when :math:`t` is structurally smaller than :math:`y`.
+ :math:`\case(c,P,f_1 … f_n)` when each :math:`f_i` is structurally smaller than :math:`y`.
  If :math:`c` is :math:`y` or is structurally smaller than :math:`y`, its type is an inductive
  type :math:`I_p` part of the inductive definition corresponding to :math:`y`.
  Each :math:`f_i` corresponds to a type of constructor
  :math:`C_q ≡ ∀ p_1 :P_1 ,~…,∀ p_r :P_r ,~∀ y_1 :B_1 ,~… ∀ y_m :B_m ,~(I_p~p_1 … p_r~t_1 … t_s )`
  and can consequently be written :math:`λ y_1 :B_1' .~… λ y_m :B_m'.~g_i`. (:math:`B_i'` is
  obtained from :math:`B_i` by substituting parameters for variables) the variables
  :math:`y_j` occurring in :math:`g_i` corresponding to recursive arguments :math:`B_i` (the
  ones in which one of the :math:`I_l` occurs) are structurally smaller than :math:`y`.


The following definitions are correct, we enter them using the :cmd:`Fixpoint`
command and show the internal representation.

.. example::

   .. rocqtop:: all

      Fixpoint plus (n m:nat) {struct n} : nat :=
      match n with
      | O => m
      | S p => S (plus p m)
      end.

      Print plus.
      Fixpoint lgth (A:Set) (l:list A) {struct l} : nat :=
      match l with
      | nil _ => O
      | cons _ a l' => S (lgth A l')
      end.
      Print lgth.
      Fixpoint sizet (t:tree) : nat := let (f) := t in S (sizef f)
      with sizef (f:forest) : nat :=
      match f with
      | emptyf => O
      | consf t f => plus (sizet t) (sizef f)
      end.
      Print sizet.

.. _Reduction-rule:

Reduction rule
++++++++++++++

Let :math:`F` be the set of declarations:
:math:`f_1 /k_1 :A_1 :=t_1 …f_n /k_n :A_n:=t_n`.
The reduction for fixpoints is:

.. math::
   (\Fix~f_i \{F\}~a_1 …a_{k_i}) ~\triangleright_ι~ \subst{t_i}{f_k}{\Fix~f_k \{F\}}_{k=1… n} ~a_1 … a_{k_i}

when the structural argument :math:`a_{k_i}` starts with a constructor.
This last restriction is needed in order to keep strong normalization
and corresponds to the reduction for primitive recursive operators.
The following reductions are now possible:

.. math::
   :nowrap:

   \begin{eqnarray*}
   \plus~(\nS~(\nS~\nO))~(\nS~\nO)~& \trii & \nS~(\plus~(\nS~\nO)~(\nS~\nO))\\
                                   & \trii & \nS~(\nS~(\plus~\nO~(\nS~\nO)))\\
                                   & \trii & \nS~(\nS~(\nS~\nO))\\
   \end{eqnarray*}

.. _Mutual-induction:

**Mutual induction**

The principles of mutual induction can be automatically generated
using the Scheme command described in Section :ref:`proofschemes-induction-principles`.
